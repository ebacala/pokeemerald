const LOCALID_BLIND_MAN = 1

mapscripts Route118_Blindmans_Home_MapScripts {
    MAP_SCRIPT_ON_RESUME: Route118_Blindmans_Home_OnResume
    MAP_SCRIPT_ON_LOAD {
        if (!flag(FLAG_VISITED_BLIND_MANS_HOME) && !defeated(TRAINER_BLIND_MAN)) {
            setobjectxyperm(LOCALID_BLIND_MAN, 4, 4)
            setobjectmovementtype(LOCALID_BLIND_MAN, MOVEMENT_TYPE_FACE_LEFT)
        }
    }
}

script Route118_Blindmans_Home_OnResume {
    if (!flag(FLAG_VISITED_BLIND_MANS_HOME) && !defeated(TRAINER_BLIND_MAN)) {
        setflag(FLAG_VISITED_BLIND_MANS_HOME)

        lock

        call(Route118_Blindmans_Home_First_Visit)
        call(Route118_Blindmans_Home_Talk_To_Blind_Man_Before_Battle)

        release
    }
}

script Route118_Blindmans_Home_First_Visit {
    playse(SE_PIN)
    applymovement(LOCALID_BLIND_MAN, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(LOCALID_BLIND_MAN, Common_Movement_FacePlayer)
    waitmovement(0)
    msgbox(format("Who is it? It has been a long time since someone visited me. Please come in."))

	applymovement(OBJ_EVENT_ID_PLAYER, Player_Walk_To_Blind_Man)
	waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    applymovement(LOCALID_BLIND_MAN, Common_Movement_FacePlayer)
    waitmovement(0)

	msgbox(format("Who are you?"), MSGBOX_NPC)
	msgbox(format("Hmm I see... You're a trainer, aren't you? And you need to learn braille? Very well, I will help you. On one condition though..."))
}

script Route118_Blindmans_Home_Talk_To_Blind_Man_Before_Battle {
    msgbox(format("Defeat me in battle and I will teach you braille. It's been a long time since I last battled, so I'm sure you'll win. Right?"), MSGBOX_YESNO)

    if (var(VAR_RESULT)) {
        trainerbattle_single(TRAINER_BLIND_MAN, Route118_Blindmans_Home_Text_Blind_Man_Battle_Intro, Route118_Blindmans_Home_Text_Blind_Man_Battle_Defeat, Route118_Blindmans_Home_Talk_To_Defeated_Blind_Man)
    } else {
        msgbox(format("Very well, Some other time then..."))
    }
}

script Route118_Blindmans_Home_Talk_To_Defeated_Blind_Man {
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(0)
    msgbox(format("I might have a book somewhere that could help you. My daughter used to use it to learn braille. Let me check..."))

    applymovement(LOCALID_BLIND_MAN, Blind_Man_Go_To_Library)
    waitmovement(0)

    msgbox(format("Let's see..."))
    msgbox(format("Ha! Here it is!"))

    applymovement(LOCALID_BLIND_MAN, Blind_Man_Go_From_Library_To_Player)
    waitmovement(0)
    applymovement(LOCALID_BLIND_MAN, Common_Movement_FacePlayer)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
    waitmovement(0)

    msgbox(format("There you go!"))
    playfanfare(MUS_OBTAIN_ITEM)
    message(format("You can now read braille!"))
    waitmessage
    waitfanfare
    msgbox(format("I'm sorry I can't see you off, but I'm sure you can find your way out. Safe travels!"))
}

script Route118_Blindmans_Home_Talk_To_Blind_Man {
    lock

    if (defeated(TRAINER_BLIND_MAN)) {
        msgbox(format("I'm sure braille will help you on your journey. Safe travels!"))
    } else {
        call(Route118_Blindmans_Home_Talk_To_Blind_Man_Before_Battle)
    }

    release
    end
}

text Route118_Blindmans_Home_Text_Blind_Man_Battle_Intro {
    format("Okay then, let's begin!")
}

text Route118_Blindmans_Home_Text_Blind_Man_Battle_Defeat {
    format("You are a worthy trainer! I'll help you!")
}

movement Player_Walk_To_Blind_Man {
	walk_up * 2
	walk_left
}

movement Blind_Man_Go_To_Library {
	walk_right * 3
    walk_up * 2
	face_up
}

movement Blind_Man_Go_From_Library_To_Player {
	walk_down * 3
    walk_left * 2
}